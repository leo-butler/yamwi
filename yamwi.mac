/* set output line length */
linel : slength(concat(maxima_tempdir,"/",%codigo_usuario%,".mac")) + 10;

/* texput */
texput(matrix,lambda([expr], printf(false,"\\begin{bmatrix}~%~{~{~a~^&~}~^\\\\~}~%\\end{bmatrix}",map(lambda([row],map(tex1,row)),args(expr)))));
texput(?rat,lambda([expr],printf(false,"\\dfrac{~a}{~a}",tex1(num(expr)),tex1(denom(expr)))));
texput("/",lambda([expr],printf(false,"\\dfrac{~a}{~a}",tex1(num(expr)),tex1(denom(expr)))));
texput(nounify(diff), lambda([expr],block([Y,X,n], [Y,X,n] : args(expr), if n=1 then printf(false,"\\dfrac{d\\,}{d{~a}}{~a}",tex1(X),tex1(Y)) else printf(false,"\\dfrac{d^{~a}\\,}{d{~a}^{~a}}{~a}",n,tex1(X),n,tex1(Y))))) ;
texput('diff, lambda([expr],block([Y,X,n], [Y,X,n] : args(expr), if n=1 then printf(false,"\\dfrac{d\\,}{d{~a}}{~a}",tex1(X),tex1(Y)) else printf(false,"\\dfrac{d^{~a}\\,}{d{~a}^{~a}}{~a}",n,tex1(X),n,tex1(Y))))) ;
tex_exp(expr) := block([b,e],
  [b,e] : args(expr),
  if e=-1 then printf(false,"\\dfrac{1}{~a} ",tex1(b))
  else if atom(b) then printf(false,"{~a}^{~a}",tex1(b),tex1(e))
  else printf(false,"\\left(~a\\right)^{~a}",tex1(b),tex1(e))) ,
texput("^",tex_exp) ,

/* add paths to package folder */

file_search_maxima:
   append (file_search_maxima,
           [concat(%dir_sources%, "/###.mac")])$

file_search_lisp:
   append (file_search_lisp,
           [concat(%dir_sources%, "/###.lisp")])$

/* Global variables */

%num_sentence%: 0$
%num_grafico%: 0$
%num_movie%: 0$
%movie_term%: 'pngcairo$

/* Movie making */
gplt_fn_seed:random(10^8);
gplt_fn() := (gplt_fn_seed:random(10^8), printf(false,"~a-maxout-~8,'0d.gnuplot",%codigo_usuario%,gplt_fn_seed));
data_fn() := printf(false,"~a-data-~8,'0d.gnuplot",%codigo_usuario%,gplt_fn_seed);

draw_multiple_plots(drawer,fname,term,lst,[opts]) := block([file,files,n:length(lst),i:0,optsf],
  files:[],
  for p in lst do (
    i:i+1,file:printf(false,"~a~8,'0d",fname,i),
    files:append(files,[file]),
    optsf:append(opts,[terminal=term,file_name=file,gnuplot_file_name=gplt_fn(),data_file_name=data_fn()]),
    apply(drawer,[p,optsf])),
  map(lambda([x],concat(x,".",term)),files));

/* see https://trac.ffmpeg.org/wiki/Slideshow */
Draw_movie(drawer,paramlist,[ffmpeg_options]) := block([cmd,files,fname,term,output_file,linel:linel],
  if ffmpeg_options=[] then ffmpeg_options:["-vf framerate=fps=30:interp_start=64:interp_end=192:scene=100"],
  drawer:subst([Draw2d=draw2d,Draw3d=draw3d,Draw=draw],drawer),
  term:'png,
  /* in yamwi.php, maxima_tempdir is hard-coded to be ./tmp */
  fname:concat("tmp/movie-still-",%codigo_usuario%,"-",%num_proceso%,"-",%num_movie%,"-"),
  output_file:concat(fname,"0.",%movie_muxer%),
  files:draw_multiple_plots(drawer,fname,%movie_term%,paramlist),
  /* ffmpeg hangs if STDIN != /dev/null */
  cmd:if %movie_is_embedded%=1 then printf(false,"/bin/sh -c '~a -y -framerate 30 -f image2 -i ~a%08d.~a ~{~a ~} -f ~a - | ~a' < /dev/null",%ffmpeg_bin%,fname,term,ffmpeg_options,%movie_muxer%,%base64_cmd%)
    else printf(false,"/bin/sh -c '~a -y -framerate 30 -f image2 -i ~a%08d.~a ~{~a ~} ~a' < /dev/null",%ffmpeg_bin%,fname,term,ffmpeg_options,output_file),
  print(concat("@@@video_",%movie_muxer%,"@@@")),
  system(cmd),
  if %movie_is_embedded%=1 then print("@@@/video@@@") else (linel:slength(output_file)+13, print(concat(output_file,"@@@/video@@@"))),
  [op(__)]);

alias(draw_movie,Draw_movie) $

/* Code for draw */
/*
Supported terminal types:
png pngcairo gif animated_gif jpg svg (default: svg)
*/

output2stdout : user_preamble = printf(false,"base64out='|~a';~%if (GPVAL_OUTPUT ne base64out) {~%unset output;~%set output '|~a';~%};",%base64_cmd%,%base64_cmd%);

mwdrawxd(func, descgraf, mutate):=
  block([formato, result, unsafe_options],
    if not get('draw,'version) then load("draw"),
    /* each unsafe option gives an attacker an opportunity to gain free access to gnuplot--hence a shell--via a trick like that below, so filter them out */
    unsafe_options: '[user_preamble,gnuplot_file_name,data_file_name],
    descgraf:sublist(descgraf,lambda([x], not(is(not(atom(x)) and op(x)="=" and member(lhs(x),unsafe_options))))),
    descgraf: append([dimensions=[400,300],
                      /* pipe the gnuplot output through base64 */
                      file_name = concat("|",%base64_cmd%,"' # "),
                      gnuplot_file_name = gplt_fn(),
                      data_file_name = data_fn()],
                      descgraf),
    /* get the terminal passed by command, if any */
    for r in descgraf do
    if not(atom(r)) and op(r)="=" and lhs(r)='terminal then formato:rhs(r),
    /* If terminal is not specified, force svg */
    if member(formato,'[png,gif,jpg,svg]) then 'done
    else if formato = 'animated_gif then formato:'gif
    else if formato = 'pngcairo then formato:'png
    else (formato : 'svg, descgraf : endcons(terminal = 'svg, descgraf)),
    print(concat("@@@draw_",formato,"@@@")),
    %num_grafico%: %num_grafico% + 1,
    result : apply(func,descgraf),
    print("@@@/draw@@@"), result)$

Draw2d([descgraf]):= mwdrawxd('draw2d, descgraf, false) $
Draw3d([descgraf]):= mwdrawxd('draw3d, descgraf, false) $
Draw([descgraf])  := mwdrawxd('draw, descgraf, true) $

/* Code for plot */
/* 
   Example controlling graphic dimensions:
   plot2d (sin(x), [x, -%pi, %pi], [gnuplot_png_term_command, "set term pngcairo size 800,800"])$
*/

mwplotxd(func, descgraf):=
  block([gptc: false, formato:"svg", cleandescgraf:[]],
    for k in descgraf do
      if not listp(k) or 
         not member(first(k), ['png_file, 'svg_file, 'pdf_file, 'ps_file, 'gnuplot_term, 'gnuplot_out_file,
                               'run_viewer ])
        then (if not atom(k) and first(k)='gnuplot_png_term_command
                then gptc:true,
              cleandescgraf: endcons(k, cleandescgraf)),
    cleandescgraf: append(cleandescgraf,
                          [[gnuplot_out_file,  concat(%codigo_usuario%,".gnuplot")],
                           [svg_file, concat(maxima_tempdir,
                                             "/",
                                             %codigo_usuario%,
                                             ".g.",
                                             %num_proceso%,
                                             ".",
                                             %num_sentence%,
                                             ".",
                                             %num_grafico%,
                                             ".svg")] ] ),
    if not gptc
      then cleandescgraf: endcons([gnuplot_svg_term_command, "set term svg size 500,400"],
                                  cleandescgraf),
    with_stdout(
      concat(maxima_tempdir,
             "/",
             %codigo_usuario%,
             ".gr.",
             %num_proceso%,
             ".",
             %num_sentence%,
             ".txt"), 
      print(concat("tmp/",
                   %codigo_usuario%,
                   ".g.",
                   %num_proceso%,
                   ".",
                   %num_sentence%,
                   ".",
                   %num_grafico%,
                   ".",
                   formato))),
    %num_grafico%: %num_grafico% + 1,
    apply(func,cleandescgraf)  ) $

Plot2d([descgraf]):= (mwplotxd('plot2d, descgraf), 'plot2d) $
Plot3d([descgraf]):= (mwplotxd('plot3d, descgraf), 'plot3d) $

/* statistical graphics */

Scatterplot([desc]) := Draw(apply('scatterplot_description, desc)) $
Histogram([desc]) := Draw2d(apply(histogram_description, desc)) $
Barsplot([desc]) := Draw2d(apply(barsplot_description, desc)) $
Piechart([desc]) := Draw2d(apply(piechart_description, desc)) $
Boxplot([desc]) := Draw2d(apply(boxplot_description, desc)) $
Starplot([desc]) := Draw2d(apply(starplot_description, desc)) $

/* direction fields package */

Drawdf([params]) := Draw(apply('df_graphics, params))$

/* output formats */

translate_into_tex(sentencias) :=
  block(
    print("start_maxima_output_tex_code:"),
    map(lambda([%%lambda_argument%%],
               %num_sentence%: %num_sentence% + 1,
               %num_grafico%: 1,
               tex(eval_string(%%lambda_argument%%))),
        sentencias)) $

translate_into_print(sentencias) :=
  block([res],
    print("start_maxima_output_print_code:"),
    map(lambda([%%lambda_argument%%],
               %num_sentence%: %num_sentence% + 1,
               %num_grafico%: 1,
               res: eval_string(%%lambda_argument%%),
               print("%%%"),
               print(res),
               print("%%%")),
        sentencias)) $
